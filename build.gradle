buildscript {
    repositories {
        jcenter()
        maven {
            url "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT"
    }
}

repositories {
    maven {
        url "http://maven.thiakil.com"
    }
    maven {
        url "https://maven.mcmoddev.com"
    }
    maven {
        url "http://dvs1.progwml6.com/files/maven"
    }
}

apply plugin: "net.minecraftforge.gradle.forge"

version = "L1-1.12.2.002"
group = "me.paulf.wings"
archivesBaseName = "Wings"
sourceCompatibility = JavaVersion.VERSION_1_8

minecraft {
    version = "1.12.2-14.23.5.2768"
    runDir = "run"

    mappings = "snapshot_20171003"
    makeObfSourceJar = false
    clientJvmArgs = serverJvmArgs = ["-Dfml.coreMods.load=me.paulf.wings.server.asm.plugin.WingsLoadingPlugin"]
}

jar {
    manifest {
        attributes "FMLCorePlugin": "me.paulf.wings.server.asm.plugin.WingsLoadingPlugin"
        attributes "FMLCorePluginContainsFMLMod": "true"
    }
}

// workaround for FML DirectoryDiscoverer limitations
sourceSets {
    main {
        output.resourcesDir = output.classesDir
    }
}

jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}
// end workaround

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    // copy everything else except the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}
